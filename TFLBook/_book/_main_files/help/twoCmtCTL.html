<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
<title>NONMEM: CTL EXAMPLE</title>
<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
<link rel="stylesheet" type="text/css" href="R.css" />
</head>
<body>
<pre>
$PROB RUN# 0069 ; 0028 but THETA 7, THETA 8, OM7, OM8 estimated.
$INPUT C STUD ID COHO SEX ROUT VISIT DOSE DV HALD INFDUR DAY TIME WGTB RACE HGTB AGE DIS MDV EVID AMT RATE ADDL II CMT
$DATA ../183_6209_620_390.csv IGNORE=C
	IGNORE=(ID.EQ.91)
  ; 1015=91 in Study 183 is a placebo 

$SUBROUTINES ADVAN4 TRANS4

$PK
    BWAVG = 76.5   	; kg.  mean for the three studies,

   MU_1=LOG(THETA(1))
   TVCL=EXP(MU_1+ETA(1))	; L/day

   MU_7=LOG(THETA(7))
   SBWCL=EXP(MU_7+ETA(7))
   
   CL=TVCL*(WGTB/BWAVG)**SBWCL	; L/day
   
   MU_2=LOG(THETA(2))
   TVV2=EXP(MU_2+ETA(2))	; L

   MU_8=LOG(THETA(8))
   SBWV2=EXP(MU_8+ETA(8))
   
   V2=TVV2*(WGTB/BWAVG)**SBWV2	; L

   MU_3=LOG(THETA(3))
   Q=EXP(MU_3+ETA(3))	; L/day

   MU_4=LOG(THETA(4))
   V3=EXP(MU_4+ETA(4))	; L

   MU_5=LOG(THETA(5))
   KA=EXP(MU_5+ETA(5))	; 1/day

   MU_6=LOG(THETA(6)/(1-THETA(6)) )
   F1=1/(1+EXP(-(MU_6+ETA(6))) )  	; %
 
   ; calculate infusion rate for IV by infusion time and dose level
   IF( INFDUR.GT.0 ) R2=DOSE/INFDUR	 	; mg/day

   S2=V2/1000 ; mg/L -> ng/mL	
 
   ; "derived"
   K=CL/V2
   K23=Q/V2
   K32=Q/V3
 
   ; beta-phase exponential decay
   BETA=(K-(((K**2+(2*((K23*(K+K32))-(K*K32)))+K23**2+K32**2)**0.5)*( 1))+K23+K32)*0.5

   ; terminal half-life 
   THAL=LOG(2)/BETA	  ; day
 
$ERROR
 DEL=0
 IF (F.LE.0.0001) DEL=1
 IPRE=F
 W1= 1
 W2= F
 IRES= DV-IPRE
 IWRE=IRES/(W1+W2)
   Y = F + W1*ERR(1) + W2*ERR(2)
 
$THETA
  (0, 0.161) ;[CL, L/day]
  (0, 3.030) ;[V2, L]
  (0, 0.930) ;[Q, L/day]
  (0, 2.09) ;[V3, L]
  (0, 0.370) ;[KA, 1/day]
  (0, 0.664) ;[F, %]
  0.75 FIXED ; [CL~BW, -]
  1 FIXED ; [V2~BW, -]

$OMEGA BLOCK(2)
  0.830 ;[P] omega(1,1)
  0.0632 ;[F] omega(2,1)
  0.104 ;[P] omega(2,2)
$OMEGA
  0 FIXED ;[P] omega(3,3)
  0 FIXED ;[P] omega(4,4)
  0.194 ;[P] omega(5,5)
  0 FIXED ;[P] omega(6,6)
  0 FIXED  ;[P] omega(7,7)
  0 FIXED   ;[P] omega(8,8)
  
$SIGMA
  0 FIXED ;[A] sigma(1,1)
  0.0320 ;[P] sigma(2,2)
  
;$EST METH=ITS INTERACTION PRINT=5 NITER=20 SIG=3 NOABORT MSFO=./0060.MSF
$EST METH=1 INTERACTION PRINT=5 MAX=9999 SIG=3 NOABORT MSFO=./0069.MSF
$EST METH=IMP INTERACTION PRINT=1 NITER=200 EONLY=0 NOABORT SIGL=9 CTYPE=3 MSFO=./0069.MSF
$EST METH=IMP INTERACTION PRINT=1 NITER=10 ISAMPLE=2000 EONLY=1 NOABORT SIGL=9 MSFO=./0069.MSF

$COV UNCONDITIONAL NOFCOV PRINT=E

$TABLE ONEHEADER NOPRINT FILE=./0069.tab
  ID TIME EVID STUD COHO SEX ROUT VISIT DOSE DAY WGTB RACE AGE DIS HGTB IPRE IRES IWRE MDV
  CL V2 Q V3 KA R2 F1 THAL SBWCL SBWV2 
  ETA1 ETA2 ETA3 ETA4 ETA5 ETA6 ETA7 ETA8
$TABLE ONEHEADER NOPRINT FIRSTONLY FILE=./0069par.tab
  ID EVID CL V2 Q V3 KA R2 F1 SBWCL SBWV2 THAL  


</pre>
</body>
</html>
